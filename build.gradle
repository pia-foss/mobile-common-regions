buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-serialization:1.4.21'
    }
}

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.4.21'
    id "org.jetbrains.kotlin.native.cocoapods" version "1.4.10"
    id 'com.android.library'
    id 'kotlin-android-extensions'
}

repositories {
    google()
    jcenter()
    mavenCentral()
}

apply plugin: 'kotlinx-serialization'

android {
    compileSdkVersion 30
    sourceSets.main.manifest.srcFile('src/androidMain/AndroidManifest.xml')
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 30
    }
}

kotlin {
    version = "1.1.0"
    cocoapods {
        summary = "Regions Module"
        homepage = "https://www.privateinternetaccess.com"
        frameworkName = "PIARegions"
    }
    targets.withType(org.jetbrains.kotlin.gradle.plugin.mpp.KotlinNativeTarget) {
        binaries.withType(org.jetbrains.kotlin.gradle.plugin.mpp.Framework) {
            isStatic = false
        }
    }

    targets {
        final def iOSPreset = System.getenv('SDK_NAME')?.startsWith("iphoneos") ? presets.iosArm64 : presets.iosX64
        fromPreset(iOSPreset, "ios") {
            binaries {
                framework("PIARegions")
            }
        }
        fromPreset(presets.android, "android")
    }

    sourceSets {
        commonMain.dependencies {
            implementation "io.ktor:ktor-client-core:$ktor_version"
            implementation "org.jetbrains.kotlin:kotlin-stdlib-common"
            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
            implementation "org.jetbrains.kotlinx:kotlinx-serialization-core:$serialization_version"
        }
        commonTest.dependencies {
            implementation "org.jetbrains.kotlin:kotlin-test"
            implementation "org.jetbrains.kotlin:kotlin-test-junit"
        }
        androidMain.dependencies {
            implementation 'com.madgag.spongycastle:core:1.54.0.0'
            implementation "io.ktor:ktor-client-okhttp:$ktor_version"
            implementation "org.jetbrains.kotlin:kotlin-stdlib"
            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
            implementation "org.jetbrains.kotlinx:kotlinx-serialization-core:$serialization_version"
        }
        iosMain.dependencies {
            implementation "io.ktor:ktor-client-ios:$ktor_version"
            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
            implementation "org.jetbrains.kotlinx:kotlinx-serialization-core:$serialization_version"
        }
    }
}